/////////////////////////////////////////////
// UO Steam Bandage macro with Hiding and Camping
// for Outlands
// 
// * loops bandaging self
// * warns if bandages low
// * warns if bandages out
// * attempts to hide
// * attempts to camp (needs kindling)
/////////////////////////////////////////////


// Create camping timer
removetimer 'camping'
createtimer 'camping'

// Create hiding timer
removetimer 'hiding'
createtimer 'hiding'

// Create healing timer
removetimer 'healing'
createtimer 'healing'

while not dead

    //
    // Bandage check
    //
    if counttype '0xe21' 'any' 'backpack' == 0
        sysmsg "No bandages!" 38
        pause 2000
        continue
    endif

    //
    // Low bandage warning
    //
    if counttype '0xe21' 'any' 'backpack' < 20
        sysmsg "Low on bandages" 55
    endif

    ///
    /// Healing
    ///
    if poisoned 'self' or diffhits > 5

        settimer 'healing' 0
        headmsg "(bandaging)" 48
        bandageself
        clearjournal

        // Loop until finished healing or 15s is reached
        while not dead
            pause 200
            if @injournal 'heal' 'system'
                break
            endif
            if @injournal 'cure' 'system'
                break
            endif
            if @injournal 'finish' 'system'
                break
            endif
            if timer 'healing' > 15000
                break
            endif
        endwhile

    endif

    //
    // Camping
    //
    if timer 'camping' > 4000
        if @findtype 0xde1 'any' 'backpack'
            useobject 'found'
            settimer 'camping' 0
        endif
    endif

    //
    // Hiding
    //
    if timer 'hiding' > 11000
        useskill 'hiding'
        settimer 'hiding' 0
    endif

    pause 1000

endwhile
