@clearjournal
settimer 'track' 11002
while not dead
  //
  // Script indicator for player
  //
  headmsg "[SAILING]" 92
  //
  // Tracking warn
  //
  if @injournal 'now tracking' 'system'
    for 5
      partymsg "FOUND PLAYER!!"
      headmsg "FOUND PLAYER!" 23
      pause 1000
    endfor
  endif
  //
  // Pause on target clearing
  //
  if targetexists
    pause 2000
    @canceltarget
  endif
  //
  // arm bow/crossbow
  //
  if not @findlayer 'self' 1
    // heavy xbow
    @findtype 0x13fd 'any' 'backpack'
    @equipitem 'found' 1
    // bow
    @findtype 0x13b2 'any' 'backpack'
    @equipitem 'found' 1
    pause 1000
  endif
  //
  // low bandages check
  //
  if counttype '0xe21' 'any' 'backpack' == 0
    headmsg "Out of bandages!" 38
    stop
  endif
  //
  // out of bandages check
  //
  if counttype '0xe21' 'any' 'backpack' < 20
    headmsg "Low on bandages" 38
    pause 1000
  endif
  //
  // attack enemy near you
  //
  @unsetalias 'enemy'
  @Getenemy 'murderer' 'enemy' 'criminal' 'gray'
  if @findobject 'enemy'
    attack! 'enemy'
  endif
  //
  // get nearest friend for heal
  //
  for 20
    @unsetalias 'friend'
    @Getfriend 'innocent' 'humanoid'
    if @inrange 'friend' 2
      pause 100
      break
    endif
    @unsetalias 'friend'
  endfor
  //
  // wait for bandage finish
  //
  if @timerexists 'healing'
    if timer 'healing' > 15000
      @removetimer 'healing'
    endif
    if @injournal 'heal' 'system'
      @removetimer 'healing'
    endif
    if @injournal 'cure' 'system'
      @removetimer 'healing'
    endif
    if @injournal 'finish' 'system'
      @removetimer 'healing'
    endif
  endif
  //
  // Do I need a bandage?
  //
  if not @timerexists 'healing'
    if poisoned 'self' or diffhits > 10
      @createtimer 'healing'
      headmsg "(bandaging)" 67
      bandageself
      @clearjournal
    endif
  endif
  //
  // Does crew need a bandage?
  //
  if not @timerexists 'healing'
    if @findobject 'friend'
      @clearjournal
      usetype 0xe21 'any' 'backpack'
      waitfortarget 5000
      target! 'friend'
      pause 500
      if not @injournal 'not damaged' 'system'
        headmsg "(bandaging crew)" 67
        createtimer 'healing'
      endif
    endif
  endif
  @clearjournal
  pause 2000
  //
  // Active track on players, click slot 2
  //
  if @timerexists 'track' and timer 'track' > 11001
    useskill "Tracking"
    waitforgump 0xfe5c638b 5000
    replygump 0xfe5c638b 5
    settimer 'track' 0
  endif
endwhile