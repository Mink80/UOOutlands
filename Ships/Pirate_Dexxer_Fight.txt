@clearjournal
while not dead
  @canceltarget
    //
  // Script indicator for player
  //
  headmsg "[FIGHT]" 92
  //
  // tracking warning
  //
  if @injournal 'now tracking' 'system'
    for 3
      headmsg "TRACKING PLAYER" 23
      pause 500
    endfor
    @clearjournal
  endif
  //
  // arm bow/crossbow
  //
  if not @findlayer 'self' 1
    // heavy xbow
    @findtype 0x13fd 'any' 'backpack'
    @equipitem 'found' 1
    // bow
    @findtype 0x13b2 'any' 'backpack'
    @equipitem 'found' 1
    pause 1000
  endif
  //
  // low bandages check
  //
  if counttype '0xe21' 'any' 'backpack' == 0
    headmsg "Out of bandages!" 38
    stop
  endif
  //
  // out of bandages check
  //
  if counttype '0xe21' 'any' 'backpack' < 20
    headmsg "Low on bandages" 38
    pause 1000
  endif
  //
  // attack enemy near you
  //
  @unsetalias 'enemy'
  @Getenemy 'murderer' 'enemy' 'criminal' 'gray'
  //
  // get nearest friend for heal
  //
  for 20
    @unsetalias 'friend'
    @Getfriend 'any' 'humanoid'
    if @inrange 'friend' 2
      pause 100
      break
    endif
    @unsetalias 'friend'
  endfor
  //
  // wait for bandage finish
  //
  if @timerexists 'healing'
    if timer 'healing' > 15000
      @removetimer 'healing'
    endif
    if @injournal 'not damaged' 'system'
      @removetimer 'healing'
    endif
    if @injournal 'heal' 'system'
      @removetimer 'healing'
    endif
    if @injournal 'cure' 'system'
      @removetimer 'healing'
    endif
    if @injournal 'finish' 'system'
      @removetimer 'healing'
    endif
  endif
  //
  // Do I need a bandage?
  //
  if not @timerexists 'healing'
    if poisoned 'self' or diffhits > 10
      @createtimer 'healing'
      headmsg "(bandaging)" 67
      bandageself
      @clearjournal
    endif
  endif
  //
  // Does crew need a bandage?
  //
  if not @timerexists 'healing'
    if @findobject 'friend'
      @clearjournal
      headmsg "(bandaging crew)" 67
      createtimer 'healing'
      usetype 0xe21 'any' 'backpack'
      waitfortarget 5000
      target! 'friend'
    endif
  endif
  //
  // throw explosion if you have them
  //
  if @timerexists 'explosion'
    //
    // reset after 11 seconds
    //
    if timer 'explosion' > 11000
      removetimer 'explosion'
    endif
  else
    if @inrange 'enemy' 12
      if not @inrange 'enemy' 4
        if counttype 0xf0d 'any' 'backpack' > 0
          findtype 0xf0d 'any' 'backpack' > 0
          useobject 'found'
          waitfortarget 5000
          target! 'enemy'
          msg "lookout!" 67
          createtimer 'explosion'
        endif
      endif
    endif
  endif
  //
  // Begging
  //
  if @timerexists 'begging'
    //
    // reset after 5 mins
    //
    if timer 'begging' > 30000
      removetimer 'begging'
    endif
  else
    @Getfriend 'friend' 'innocent'
    if @inrange 'friend' 15
      useskill 'Begging'
      waitfortarget 5000
      target! 'friend'
      createtimer 'begging'
    endif
  endif
  pause 1000
endwhile