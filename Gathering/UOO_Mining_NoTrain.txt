/////////////////////////////////////////////
// UO Steam Mining Macro
// for Outlands
//
// * just mines ore, nothing else
// * clicks ore in case you near forge
/////////////////////////////////////////////

//
// Clear hands
//
clearhands 'both'
pause 1000

//
// Whistle timer
// 
removetimer 'whistle'
createtimer 'whistle'

//
// Clear alias (possibly other macros)
// 
@unsetalias 'PickAxe'

//
// Create Messages list
//
@removelist 'Messages'
@createlist 'Messages'

//
// While alive
//
while hits > 0

    //
    // Check weight
    // 
    if diffweight < 10
        headmsg 'I am too heavy, please unload me' '23'
        pause 5000
        continue    
    endif

    //
    // Check ore cache
    // 
    if diffweight < 100
        while @findtype 0x19b9 'any' 'backpack'
            useobject 'found'
            pause 500
        endwhile
    endif

    // 
    // Check holding pickaxe
    // 
    if not @findlayer 'self' 1
        pause 1000
        @unsetalias 'Pickaxe'
    endif

    // 
    // Equip pickaxe or stop with msg
    // 
    if not findalias 'Pickaxe'
        if @findtype 0xe86 'any' 'backpack'
            @setalias 'Pickaxe' 'found'
            // This equips it
            useobject 'Pickaxe'
            pause 1000
        else
            headmsg 'No Pickaxe found' '55' 'self'
            stop
        endif
    endif

    if counttype '0xe86' 'any' 'backpack' == 0
        sysmsg "On last Pick!" 38
    endif

    canceltarget
    clearjournal

    //
    // Mine ore
    //
    useobject 'Pickaxe'
    waitfortarget 5000
    target! 'self'
    pause 100

    // Wait for results
    for 60
        if @injournal 'You dig' 'system'
            break
            elseif @injournal "You loosen" "system"
            break
        elseif @injournal "You find" "system"
            msg "well look at that..."
            break
        elseif @injournal "harvestable" "system"
            headmsg 'Find a new spot' '43'
            pause 1000
            break
        elseif not @findlayer 'self' 1
            headmsg 'NO PICK EQUIPPED' '23'
            break
            else
            pause 100
        endif
    endfor
    
    //
    // Whistle
    //
    if timer 'whistle' > 20000
        emotemsg 'whistles'
        settimer 'whistle' 0
    endif

endwhile