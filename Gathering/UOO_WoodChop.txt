/////////////////////////////////////////////
// UO Steam Lumber Macro
// for Outlands
// v1.1
//
// * equips axe and chops lumber
// * detects overweight
// * uses camping during lumber chop but on a different
//   timer than the woodchopping since you cannot camp
//   as fast as you can chop. 
// * cuts one kindling with start dagger if it has none
//   keeping 1 in pack at all times for the next camp
// * weaves hiding into lumber and camping  - might be useful
// * if you are full weight, you will be asked to unload. Its dangerous
//   to overload yourself (cannot run from PKs or monsters). So either drop
//   on ground or runner.
// * script will check for 3 consecutive messages for no lumber, since you can 
//   get one message from one tree, but be standing near another tree and be
//   able to continue chopping without having to move.
//
// v1.2
//
// * converts logs to boards automatically
// * will make kindling out of logs using a saw, if a saw is found. At first,
//   when you have no boards, it will use dagger.
//
// [IMPORTANT! Make sure your wood craft source wood is 'Regular']
//
/////////////////////////////////////////////

//
// Create camping timer
//
if not timerexists 'camping'
    createtimer 'camping'
endif

//
// Create hiding timer
//
if not timerexists 'hiding'
    createtimer 'hiding'
endif

//
// Clear hands
//
clearhands 'both'
pause 1000

//
// Create Messages list
//
@removelist 'Messages'
@createlist 'Messages'

//
// While alive
//
while hits > 0

    //
    // Check weight
    // 
    if diffweight < 10
        headmsg 'I am too heavy, please unload me' '23'
        pause 5000
        continue    
    endif

    // 
    // Check holding hatchet
    // 
    if not @findlayer 'self' 2
        @unsetalias 'Hatchet'
    endif

    // 
    // Equip hatchet or stop with msg
    // 
    if not findalias 'Hatchet'
        if @findtype 0xf44 'any' 'backpack'
            @setalias 'Hatchet' 'found'
            // This equips it
            useobject 'Hatchet'
            pause 1000
        else
            headmsg 'No Hatchet found' '55' 'self'
            stop
        endif
    endif

    canceltarget
    clearjournal

    //
    // Chop Wood
    //
    useobject 'Hatchet'
    waitfortarget 5000
    target! 'self'
    pause 100

    // 
    // Convert logs into boards
    //
    @usetype '0x1bdd'  

    //
    // If working on camping skill
    //
    if skill 'camping' < 100
        //
        // Check if we need kindling, try to use saw
        //
        if counttype 0xde1 '0' 'backpack' < 1
            //
            // If we have a saw
            //
            if @findtype 0x1034 'any' 'backpack'
                //
                // If we have boards
                //
                if @counttype 0x1bd7 0 'backpack' > 0
                    @usetype 0x1034 'any' 'backpack'
                    pause 1000
                    replygump 0x38920abd 100
                    pause 1000
                    replygump 0x38920abd 200
                    pause 1000
                    replygump 0x38920abd 0
                    pause 1000
                    replygump 0x38920abd 0
                    pause 1000
                    replygump 0x38920abd 0
                    pause 1000
                endif
            endif
        endif

        //
        // Backup kindling method : dagger
        //
        if counttype 0xde1 '0' 'backpack' < 1
            if @findtype 0xf52 'any' 'backpack'
                useobject 'found'
                waitfortarget 5000
                target! 'self'
                pause 500
            endif
        endif

        //
        // Camp once every 3 seconds
        //
        if timer 'camping' > 3000
            if @findtype 0xde1 'any' 'backpack'
                useobject 'found'
                settimer 'camping' 0
            endif
        endif

    endif

    //
    // Try to hide every 11 seconds
    //
    if skill 'hiding' < 100
        if timer 'hiding' > 11000
            useskill 'hiding'
            settimer 'hiding' 0
        endif
    endif

    //
    // Wait for results
    //
    for 60
        if @injournal 'enough wood here' 'system'
            pushlist 'Messages' 'You need to move'
            break
        elseif @injournal 'You chop' 'system'
            break
        elseif @injournal 'fail to' 'system'
            break
        elseif @injournal 'You can' 'system'
            break
        elseif @injournal 'harvestable' 'system'
            pushlist 'Messages' 'You need to move'
            break
        elseif not @findlayer 'self' 2
            headmsg 'NO AXE EQUIPPED' '23'
            break
        else
            pause 100
        endif
    endfor

    //
    // See if character really needs to move or not
    // 
    if list 'Messages' > 2
        headmsg 'Find another tree!' 44
        removelist 'Messages'
        createlist 'Messages'
        pause 3000
    endif

endwhile