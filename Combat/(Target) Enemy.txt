/////////////////////////////////////////////
// UO Steam Attack Enemy Macro
// for Outlands
//
// This macro _attacks_ your 'enemy' alias, or, if you have
// a spell queue active and your 'enemy' is in range,
// drops your target queue on that target.
//
// This is useful for an archer mage (example) - cast explosion, 
// leave it primed, run this macro - it will drop the spell
// on your 'enemy' target, run your dress macro (for bow),
// then attack your target for bow shot.
//
// If you don't have spell casting you can still use this,
// it will just skip the target checks. Casters can decide
// whether or not they want to go into war mode (or just remove it)
//
// Warning : do not run this if you have a friendly spell up!
//
// How to use:
// ===========
// 1) Save your current equipment as 'default'
//    in dress macro. 
// 2) Run script any time to attack your 'enemy' target.
//
// Version History:
// ================
// * 1.0 initial release
// * 1.1 holds target while up
//
// COPY HERE >>
/////////////////////////////////////////////

//
// run dress macro
//
dress 'default'
while dressing
endwhile

//
// check 'enemy' alias and attack
//
if @findobject 'enemy'

    // 
    // drop queued spell if enemy in range
    //
    while targetexists 
        if @inrange 'enemy' 15 and not dead 'enemy'
            target! 'enemy'
            break
        else
            headmsg "too far!" 44
        endif    
        pause 500    
    endwhile

    //
    // if you are a pure caster, take this part out
    //
    attack! 'enemy'
    if not war 'self'
        warmode 'on'
    endif
else
    headmsg "No targets" 44
endif
@clearusequeue
@cancelautotarget