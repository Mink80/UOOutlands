/////////////////////////////////////////////
// UO Steam Get Closest Macro
// for Outlands
//
// This macro _gets_ your closest target,
// prioritizing targets by your color (red, blue),
// and sets that target to alias 'enemy'. Recommend
// you pair this script with 'attackenemy.txt'
//
// Recommendation:
// ===============
// [MOUSEDOWN] = 'getenemy.txt'
// [MOUSEUP] = 'attackenemy.txt'
// 
// Only use this if you have another script which
// actually attacks your 'enemy' alias. If you want
// a script which gets and attacks, use the 
// 'attackclosest.txt' script.
//
// Version History:
// * 1.0 initial release
//
/////////////////////////////////////////////

//
// Run search loop for all targets and insert into list
// 
@removetimer 'search'
@createtimer 'search'
@removelist 'found'
@createlist 'found'
for 0 to 20
    @unsetalias 'enemy'
    if murderer 'self'
        getenemy 'murderer' 'innocent' 'enemy' 'criminal' 'gray' 'humanoid' 'transformation' 'random'
    else
        getenemy 'murderer' 'enemy' 'criminal' 'gray' 'humanoid' 'transformation' 'random'
    endif
    @pushlist! 'found' 'enemy'
endfor
@removetimer 'search'

//
// Create a distances list, seeding from 1
// 
@removelist 'distances'
@createlist 'distances'
pushlist 'distances' 1
pushlist 'distances' 2
pushlist 'distances' 3
pushlist 'distances' 4
pushlist 'distances' 5
pushlist 'distances' 6
pushlist 'distances' 7
pushlist 'distances' 8
pushlist 'distances' 9
pushlist 'distances' 10
pushlist 'distances' 11
pushlist 'distances' 12
pushlist 'distances' 13
pushlist 'distances' 14
pushlist 'distances' 15

//
// Sort by distance
// with closest first
//  
@removelist 'targets'
@createlist 'targets'
for 0 to 'distances'
    for 0 to 'found'
        if @findobject found[] 0 0 0 distances[]
            @pushlist! 'targets' found[]
        endif
    endfor
endfor

//
// Set alias 'enemy' to target
//
if list 'targets' > 0
    headmsg "*Ready*" 22
    setalias 'enemy' targets[0]
else
    headmsg "No targets" 44
endif