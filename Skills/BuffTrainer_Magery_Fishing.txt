/////////////////////////////////////////////
// UO Steam Recall Skill Buff Trainer
// for Outlands
// by Jewele
// 
// !MINOC SYNDICATE ONLY!
//
// Recalls you to a marked location to kill a bird,
// back to a marked training spot, trains, and 
// when needed, recalls you to a marked bank rune
// to restock scriptregs.
//
// This is the FLAMESTRIKE version. Make sure you mark
// a rune to the Prevalia zoo and check the serial of the
// mob u want to nuke.
// 
// How to use?
// -----------
// 1) Mark a rune at a bank
// 2) Mark a rune at a bird spawn
// 3) Place these 2 runes in your backpack
// 4) Put your boat rune in your bank
// 6) Start at -bank-, with banker in sight
// 7) Run script
//
// Version 1.0
// -----------
// First release! yay!
//
// Version 1.1
// -----------
// Added cancel target after trying to kill a bird
//
// Version 1.2
// -----------
// Will ignore a bird it cant kill
//
// Version 1.3
// -----------
// Clears target before spellcast
//
// Version 1.4
// -----------
// Added guards against server save issue
//
// Version 1.5
// -----------
// Bumped recall regs up to 50 from 10
// Added double request for bank
//
// Version 1.6
// -----------
// Will now check bird flag before killing
//
// UOSTEAM COPY-PASTE BELOW THIS LINE
/////////////////////////////////////////////

@useobject 'backpack'
pause 1000

if not findalias 'bankrune'
    sysmsg "Select rune for bank access"
    promptalias 'bankrune'
endif
if not findalias 'birdrune'
    sysmsg "Select rune for bird killing"
    promptalias 'birdrune'
endif
if not findalias 'shiprune'
    sysmsg "Select shiprune"
    promptalias 'shiprune'
endif
if not findalias 'bankbag'
    msg "bank"
    pause 1000
    sysmsg "Select a bag with reagents in your bank"
    promptalias 'bankbag'
endif
if not findalias 'banker'
    sysmsg "Select the banker at your bank"
    promptalias 'banker'
endif

//
// Actions list
//
@removelist 'actions'
@createlist 'actions'
pushlist 'actions' 'restock'

//
// Script Regs (stocks 10)
// 
@removelist 'scriptregs'
@createlist 'scriptregs'
pushlist 'scriptregs' 0xf7a // pearl
pushlist 'scriptregs' 0xf7b // moss
pushlist 'scriptregs' 0xf86 // root
pushlist 'scriptregs' 0xf8c // ash

msg "bank"
pause 1000
msg "bank"
pause 1000

while not dead

    // 
    // Reg check
    // 
    if not @inlist! 'actions' 'restock'
        // script regs
        for 0 to 'scriptregs'
            if @counttype scriptregs[] 'any' 'backpack' < 5
                @clearlist 'actions'
                sysmsg "I need to restock scriptregs" 44
                pushlist 'actions' 'restock'
                continue
            endif
        endfor
    endif

    //
    // Restock regs
    //
    if @inlist! 'actions' 'restock'
        sysmsg "Checking and restocking regs" 44
        pause 200
        @clearlist 'actions'
        // check if we are at bank
        if not inrange 'banker' 30
            // mana to recall
            while mana < 15
                useskill 'meditation'
                pause 6000
            endwhile
            // recall
            @canceltarget
            cast "Recall"
            waitfortarget 5000
            target! 'bankrune'
            pause 1000
        endif

        // check if we are at bank
        if @inrange 'banker' 30
            msg "bank"
            resync
            pause 2000
            @useobject 'bankbag'
            pause 2000
            //
            // Reg restocking is extremely unreliable!
            //
            for 0 to 'scriptregs'
                removetimer 'restock'
                createtimer 'restock'
                while counttype scriptregs[] 'any' 'backpack' < 50
                    sysmsg "restocking reagent"
                    movetype scriptregs[] 'bankbag' 'backpack' 0 0 0 'any' 50
                    pause 1500
                    if timer 'restock' > 30000
                        sysmsg "I cannot restock the script regs"
                        stop
                    endif
                endwhile
            endfor
            pushlist 'actions' 'recalltrain'    
            continue
        else
            sysmsg "Either start at bank or start with scriptregs in pack to recall to bank"
            stop
        endif
    endif

    // 
    // Buff check
    // 
    if not @inlist! 'actions' 'buff'
        if not buffexists 'Arcane Empowerment'
            @clearlist 'actions'
            sysmsg "I need to get my skill buff" 44
            pushlist 'actions' 'buff'
            continue
        endif
    endif

    //
    // Get buff
    //
    if @inlist! 'actions' 'buff'
        sysmsg "Getting skill buff" 44
        pause 200
        @clearlist 'actions'
        // mana to recall
        while mana < 15
            useskill 'meditation'
            pause 6000
        endwhile
        // recall
        @canceltarget
        cast "Recall"
        waitfortarget 5000
        target! 'birdrune'
        pause 1000
        // mana to cast lightning
        while mana < 15
            useskill 'meditation'
            pause 6000
        endwhile
        // get buff
        while not buffexists 'Arcane Empowerment'
            @getenemy 'enemy' 'gray' 'criminal' next
            if @inrange 'enemy' 12
                if graphic 'enemy' == '0x6'
                    @canceltarget
                    cast 'Lightning'
                    waitfortarget 5000
                    target! 'enemy'
                endif
            endif
            pause 2000
        endwhile
        // mana to recall
        while mana < 15
            useskill 'meditation'
            pause 6000
        endwhile
        @pushlist 'actions' 'recalltrain'
        continue
    endif

    //
    // Recall train
    //
    if @inlist! 'actions' 'recalltrain'
        sysmsg "Heading to ship" 44
        pause 200
        @clearlist 'actions'
        // mana to recall
        while mana < 15
            useskill 'meditation'
            pause 6000
        endwhile
        // recall
        @canceltarget
        cast "Recall"
        waitfortarget 5000
        target! 'bankrune'
        pause 2000
        msg "bank"
        pause 2000
        msg "bank"
        pause 2000
        // mana to recall
        while mana < 15
            useskill 'meditation'
            pause 6000
        endwhile
        // recall
        @canceltarget
        cast "Recall"
        waitfortarget 5000
        target! 'shiprune'
        pause 2000
        pushlist 'actions' 'train'  
        continue
    endif

    // 
    // Train
    // 
    if @inlist! 'actions' 'train'
        pause 200
        @clearlist 'actions'
        @clearjournal
        //
        // INSERT TRAINING SEQUENCE HERE
        //
        // START > 
        while buffexists 'Arcane Empowerment'
            // pole
            if @findlayer 'self' 2
                setalias 'pole' 'found'
            else
                if not @findtype 0xdc0 'any' 'backpack' 
                    headmsg 'get a pole' 
                    stop
                endif
                setalias 'pole' 'found' 
                equipitem 'pole' 2
                pause 1000
            endif 
            @canceltarget
            if useobject 'pole' 
                waitfortarget 2000 
                targettileoffset! -5 -5 -5 
                pause 9000 
            endif 
            @canceltarget
            if useobject 'pole' 
                waitfortarget 2000 
                targettileoffset! -5 5 -5 
                pause 9000 
            endif 
            @canceltarget
            if useobject 'pole' 
                waitfortarget 2000 
                targettileoffset! 5 -5 -5 
                pause 9000 
            endif 
            @canceltarget
            if useobject 'pole' 
                waitfortarget 2000 
                targettileoffset! 5 5 -5 
                pause 9000 
            endif 
            if @injournal 'biting here' 'system'
                for 15 
                    msg 'forward one' 
                    pause 1000 
                endfor 
            endif
        endwhile
        // < END
        pushlist 'actions' 'train'
    endif

    pause 200

    //
    // Server save fix
    // 
    pushlist 'actions' 'train'


endwhile